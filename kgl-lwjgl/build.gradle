plugins {
    id 'org.jetbrains.kotlin.jvm'
    id 'maven-publish'
}

repositories {
    mavenCentral()
}

def lwjglVersion = "3.2.3"

dependencies {
    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.5.31'

    implementation "org.lwjgl:lwjgl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-assimp:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-glfw:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-openal:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-opengl:$lwjglVersion"
    implementation "org.lwjgl:lwjgl-stb:$lwjglVersion"

    implementation project(':kgl')
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    publications {
        all {
            pom.withXml {
                Node pomNode = asNode()
                pomNode.dependencies.'*'.findAll() {
                    it.artifactId.text() == 'kgl'
                }.each() {
                    it.parent().remove(it)
                }
            }
        }

        maven(MavenPublication) {
            groupId = 'com.danielgergely.kgl'
            artifactId = 'kgl-lwjgl'
            version = "$rootProject.ext.currentVersion"

            from components.java
            artifact sourcesJar
        }
    }
}
